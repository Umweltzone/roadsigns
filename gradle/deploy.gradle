// javadocJar and sourcesJar tasks pick up group and version
group = project.groupId
version = project.versionName

Properties properties = new Properties()
def propertiesFile = project.rootProject.file("${System.properties['user.home']}/.gradle/gradle.properties")
if (propertiesFile.exists()) {
    properties.load(propertiesFile.newDataInputStream())
}
def bintrayUser = properties.getProperty("bintrayUser") ?: System.getenv("BINTRAY_USER")
def bintrayApiKey = properties.getProperty("bintrayApiKey") ?: System.getenv("BINTRAY_API_KEY")

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = project.groupId
                artifactId = project.artifactId
                version = project.versionName

                artifact sourcesJar {
                    classifier "sources"
                }

                artifact javadocJar {
                    classifier "javadoc"
                }

                pom {
                    name = project.packageName
                    description = project.packageDescription
                    url = project.packageWebsiteUrl
                    licenses {
                        license {
                            name = project.packageLicenseApacheName
                            url = project.packageLicenseApacheUrl
                        }
                    }
                    scm {
                        connection = project.packageScmConnection
                        developerConnection = project.packageScmDeveloperConnection
                        url = project.packageWebsiteUrl
                    }
                }

                repositories {
                    maven {
                        name project.bintrayPublicationName
                        url = uri(project.bintrayUrl)
                        credentials {
                            username = bintrayUser
                            password = bintrayApiKey
                        }
                    }
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    group "publishing"
    description "Generates source jar"
    archiveClassifier.set("sources")
    from android.sourceSets.main.java.srcDirs
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    group "publishing"
    description "Generates javadocJar based on Dokka"
    archiveClassifier.set("javadoc")
    from dokkaJavadoc.outputDirectory
}

dokkaJavadoc.configure {
    moduleName.set("Roadsigns")
    dokkaSourceSets {
        named("main") {
            sourceLink {
                localDirectory.set(file("roadsigns/src/main/kotlin"))
                remoteUrl.set(java.net.URL("https://github.com/Umweltzone/roadsigns/blob/master/roadsigns/src/main/kotlin"))
                remoteLineSuffix.set("#L")
            }
        }
    }
}

artifacts {
    archives javadocJar
    archives sourcesJar
}
